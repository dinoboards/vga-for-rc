Name     U1 ;
PartNo   ATF1502 ;
Date     01/28/24 ;
Revision 01 ;
Designer Dean ;
Company  Dinoboards ;
Assembly None ;
Location Here ;
Device   F1502PLCC44 ;

/*
| Device Name Package      | Type    | JTAG Enabled    | JTAG Disabled |
| --------------------     | -----   | ----            | ----          |
| ATF1502AS/ASL/ASV        | PLCC44  | F1502ISPPLCC44  | F1502PLCC44   |
| ATF1502AS/ASL/ASV        | TQFP44  | F1502ISPTQFP44  | F1502TQFP44   |
| ATF1504AS/ASL/ASV/ASVL   | PLCC44  | F1504ISPPLCC44  | F1504PLCC44   |
| ATF1504AS/ASL/ASV/ASVL   | TQFP44  | F1504ISPTQFP44  | F1504TQFP44   |
| ATF1504AS/ASL/ASV/ASVL   | PLCC84  | F1504ISPPLCC84  | F1504PLCC84   |
| ATF1504AS/ASL/ASV/ASVL   | TQFP100 | F1504ISPTQFP100 | F1504TQFP100  |
| ATF1508AS/ASL/ASV/ASVL   | PLCC84  | F1508ISPPLCC84  | F1508PLCC84   |
| ATF1508AS/ASL/ASV/ASVL   | TQFP100 | F1508ISPTQFP100 | F1508TQFP100  |
| ATF1508AS/ASL/ASV/ASVL   | PQFP100 | F1508ISPQFP100  | F1508QFP100   |
*/

property   atmel {cascade_logic  on   };
property   atmel {fast_inlatch   on   };
property   atmel {foldback_logic on   };
property   atmel {logic_doubling on   };
property   atmel {optimize       on   };
property   atmel {output_fast    on   };
property   atmel {pin_keep       off  };
property   atmel {preassign      keep };
property   atmel {security       off  };
property   atmel {xor_synthesis  on   };
property   atmel {jtag           off  };

/** INPUTS **/
PIN 43 = CLOCK;
PIN 1 = VSYNC;
PIN 2 = VBLANK;
PIN 4 = HBLANK;
PIN 36 = VMREQ_n;
PIN 37 = WR_n;
PIN 38 = RD_n;
PIN 44 = VIOREQ;

RD = !RD_n;
WR = !WR_n;

/* INOUTS */
PIN 14 = BD0;
PIN 17 = BD1;
PIN 16 = BD2;
PIN 18 = BD3;
PIN 19 = BD4;
PIN 20 = BD5;
PIN 21 = BD6;
PIN 24 = BD7;

PIN 25 = RD0;
PIN 26 = RD1;
PIN 27 = RD2;
PIN 28 = RD3;
PIN 29 = RD4;
PIN 31 = RD5;
PIN 32 = RD6;
PIN 33 = RD7;

/* OUTPUTS */
PIN 11 = VD0;
PIN 13 = VD1;
PIN 5 = VD2;
PIN 9 = VD3;
PIN 12 = VD4;
PIN 7 = VD5;
PIN 8 = VD6;
PIN 6 = VD7;
PIN 34 = INT_n;
PIN 39 = VMREQ_WR_n;
PIN 40 = LOW_RES;

VMREQ_WR_n = !VMREQ_WR;
VMREQ_RD = !VMREQ_n & RD;
VMREQ_WR = !VMREQ_n & WR;
VMREQ = VMREQ_RD # VMREQ_WR;

VIOREQ_RD = VIOREQ & RD;
VIOREQ_WR = VIOREQ & WR;

LOW_RES.D = BD7;
LOW_RES.CK = VIOREQ_WR;

/* VSYNC INTERRUPT SIGNALLING */

/* when VSYNC goes low, INT needs to go low and have its output enabled */
/* when VIOREQ goes low, INT's value needs to be sent to CPU */
/* when VIOREQ goes high, INT needs to be de-activated */

NODE VSYNC_P;
VSYNC_P.D = VSYNC;
VSYNC_P.CK = CLOCK;
VSYNC_NEG_EDGE = VSYNC_P & !VSYNC;

NODE S0;
NODE S1;
NODE S2;

/* STATE 0: VSYNC TRIGGERED */
S0.D = (S0 # VSYNC_NEG_EDGE) & !S1 & !S2;
S0.CK = CLOCK;
/* STATE 1: TRANSITION TO S1 WHEN CPU IS NOT DOING I/O READ */
S1.D = S0 & !VIOREQ_RD;
S1.CK = CLOCK;
/* STATE 2: TRANSITION TO S2 WHEN CPU IS DOING I/O READ */
S2.D = S1 & VIOREQ_RD;
S2.CK = CLOCK;
/* STATE 3: CPU HAS COMPLETED READ, INT IS CLEARED, S0..2 ARE ALL NOW FALSE */

INT_n.D = !(S1 # S2);
INT_n.CK = CLOCK;
INT_n.OE = !INT_n;


/* DATA BUS DIRECTIONING */

BD_OUTPUT_EN = VIOREQ_RD # VMREQ_RD;
BD0 = (VIOREQ_RD & INT_n) # (VMREQ_RD & RD0);
BD0.OE = BD_OUTPUT_EN;

BD1 = (VMREQ_RD & RD1);
BD1.OE = BD_OUTPUT_EN;

BD2 = (VMREQ_RD & RD2);
BD2.OE = BD_OUTPUT_EN;

BD3 = (VMREQ_RD & RD3);
BD3.OE = BD_OUTPUT_EN;

BD4 = (VMREQ_RD & RD4);
BD4.OE = BD_OUTPUT_EN;

BD5 = (VMREQ_RD & RD5);
BD5.OE = BD_OUTPUT_EN;

BD6 = (VMREQ_RD & RD6);
BD6.OE = BD_OUTPUT_EN;

BD7 = (VMREQ_RD & RD7) # (VMREQ_RD & LOW_RES);
BD7.OE = BD_OUTPUT_EN;

/* VRAM BUS MULTIPLEXING */
/*RAM_WE_n = VMREQ_WR_n;*/

RD0 = BD0;
RD0.OE = VMREQ_WR;

RD1 = BD1;
RD1.OE = VMREQ_WR;

RD2 = BD2;
RD2.OE = VMREQ_WR;

RD3 = BD3;
RD3.OE = VMREQ_WR;

RD4 = BD4;
RD4.OE = VMREQ_WR;

RD5 = BD5;
RD5.OE = VMREQ_WR;

RD6 = BD6;
RD6.OE = VMREQ_WR;

RD7 = BD7;
RD7.OE = VMREQ_WR;

/* VIDEO RGB VALUE OUTPUTS */

VD0.D = (HBLANK & VBLANK) & ((RD0) # (VMREQ & VD0));
VD0.CK = CLOCK;
VD1.D = (HBLANK & VBLANK) & ((RD1) # (VMREQ & VD1));
VD1.CK = CLOCK;
VD2.D = (HBLANK & VBLANK) & ((RD2) # (VMREQ & VD2));
VD2.CK = CLOCK;
VD3.D = (HBLANK & VBLANK) & ((RD3) # (VMREQ & VD3));
VD3.CK = CLOCK;
VD4.D = (HBLANK & VBLANK) & ((RD4) # (VMREQ & VD4));
VD4.CK = CLOCK;
VD5.D = (HBLANK & VBLANK) & ((RD5) # (VMREQ & VD5));
VD5.CK = CLOCK;
VD6.D = (HBLANK & VBLANK) & ((RD6) # (VMREQ & VD6));
VD6.CK = CLOCK;
VD7.D = (HBLANK & VBLANK) & ((RD7) # (VMREQ & VD7));
VD7.CK = CLOCK;


