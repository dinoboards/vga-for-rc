Name     U1 ;
PartNo   ATF1502 ;
Date     01/28/24 ;
Revision 01 ;
Designer Dean ;
Company  Dinoboards ;
Assembly None ;
Location Here ;
Device   F1502PLCC44 ;

/*
| Device Name Package      | Type    | JTAG Enabled    | JTAG Disabled |
| --------------------     | -----   | ----            | ----          |
| ATF1502AS/ASL/ASV        | PLCC44  | F1502ISPPLCC44  | F1502PLCC44   |
| ATF1502AS/ASL/ASV        | TQFP44  | F1502ISPTQFP44  | F1502TQFP44   |
| ATF1504AS/ASL/ASV/ASVL   | PLCC44  | F1504ISPPLCC44  | F1504PLCC44   |
| ATF1504AS/ASL/ASV/ASVL   | TQFP44  | F1504ISPTQFP44  | F1504TQFP44   |
| ATF1504AS/ASL/ASV/ASVL   | PLCC84  | F1504ISPPLCC84  | F1504PLCC84   |
| ATF1504AS/ASL/ASV/ASVL   | TQFP100 | F1504ISPTQFP100 | F1504TQFP100  |
| ATF1508AS/ASL/ASV/ASVL   | PLCC84  | F1508ISPPLCC84  | F1508PLCC84   |
| ATF1508AS/ASL/ASV/ASVL   | TQFP100 | F1508ISPTQFP100 | F1508TQFP100  |
| ATF1508AS/ASL/ASV/ASVL   | PQFP100 | F1508ISPQFP100  | F1508QFP100   |
*/

property   atmel {cascade_logic  off  };
property   atmel {fast_inlatch   on   };
property   atmel {foldback_logic on   };
property   atmel {logic_doubling on   };
property   atmel {optimize       on   };
property   atmel {output_fast    on   };
property   atmel {pin_keep       off  };
property   atmel {preassign      keep };
property   atmel {security       off  };
property   atmel {xor_synthesis  on   };
property   atmel {jtag           off  };


/** inputs **/
PIN 43 = HSYNC;

PIN 1 = LOW_RES;
PIN 2 = LINES_REDUCED;
PIN 4 = CS0_n;
PIN 7 = BA10;
PIN 8 = BA11;
PIN 9 = BA12;
PIN 11 = BA13;
PIN 12 = BA14;
PIN 13 = BA15;
PIN 14 = BA16;
PIN 16 = BA17;
PIN 18 = BA18;
PIN 20 = BA19;
PIN 21 = BA20;
PIN 24 = BA21;
PIN 25 = BA22;
PIN 26 = BA23;

CS0 = !CS0_n;

/** outputs **/
PIN 27 = RA10;
PIN 28 = RA11;
PIN 29 = RA12;
PIN 31 = RA13;
PIN 32 = RA14;
PIN 33 = RA15;
PIN 34 = RA16;
PIN 36 = RA17;
PIN 37 = RA18;

PIN 38 = VBLANK;
PIN 39 = VSYNC;
PIN 40 = VMREQ_n;
PIN 41 = VINT_n;

/*F8 0000 TO FF FFFF*/
VRAM_RANGE = BA23 & BA22 & BA21 & BA20 & BA19;

VMREQ_n = !(CS0 & VRAM_RANGE);
VMREQ = !VMREQ_n;

PINnode 604 =  P0;
PINnode 601 =  P1;
PINnode 610 =  P2;
PINnode 602 =  P3;
PINnode 612 =  P4;
PINnode 603 =  P5;
PINnode 606 =  P6;
PINnode 605 =  P7;
PINnode 608 =  P8;
PINnode 614 =  P9;

FUNCTION MUX_BUS(BA, M0, M1) {
  MUX_BUS = ((!VBLANK # VMREQ) & BA) # (!VMREQ & ((!LOW_RES & M0) # (LOW_RES & M1)));
}
RA9.OE = LOW_RES & !VMREQ;
RA9 = LOW_RES & P1;
RA10 = MUX_BUS(BA10, P0, P2);
RA11 = MUX_BUS(BA11, P1, P3);
RA12 = MUX_BUS(BA12, P2, P4);
RA13 = MUX_BUS(BA13, P3, P5);
RA14 = MUX_BUS(BA14, P4, P6);
RA15 = MUX_BUS(BA15, P5, P7);
RA16 = MUX_BUS(BA16, P6, P8);
RA17 = MUX_BUS(BA17, P7, P9);
RA18 = (VMREQ & BA18) # (!VMREQ & (!LOW_RES & P8));

FIELD VCOUNTER = [P0..P9];
VCOUNTER.CK = HSYNC;

$DEFINE COUNT_MAX (VCOUNTER:['d'525])

/* Modified 10-bit counter with reset at 525 */
P0.D = (!COUNT_MAX & !P0);
P1.D = (!COUNT_MAX & ((P0 & !P1) # (!P0 & P1)));
P2.D = (!COUNT_MAX & ((P0 & P1 & !P2) # (!(P0 & P1) & P2)));
P3.D = (!COUNT_MAX & ((P0 & P1 & P2 & !P3) # (!(P0 & P1 & P2) & P3)));
P4.D = (!COUNT_MAX & ((P0 & P1 & P2 & P3 & !P4) # (!(P0 & P1 & P2 & P3) & P4)));
P5.D = (!COUNT_MAX & ((P0 & P1 & P2 & P3 & P4 & !P5) # (!(P0 & P1 & P2 & P3 & P4) & P5)));
P6.D = (!COUNT_MAX & ((P0 & P1 & P2 & P3 & P4 & P5 & !P6) # (!(P0 & P1 & P2 & P3 & P4 & P5) & P6)));
P7.D = (!COUNT_MAX & ((P0 & P1 & P2 & P3 & P4 & P5 & P6 & !P7) # (!(P0 & P1 & P2 & P3 & P4 & P5 & P6) & P7)));
P8.D = (!COUNT_MAX & ((P0 & P1 & P2 & P3 & P4 & P5 & P6 & P7 & !P8) # (!(P0 & P1 & P2 & P3 & P4 & P5 & P6 & P7) & P8)));
P9.D = (!COUNT_MAX & ((P0 & P1 & P2 & P3 & P4 & P5 & P6 & P7 & P8 & !P9) # (!(P0 & P1 & P2 & P3 & P4 & P5 & P6 & P7 & P8) & P9)));

/* VBLANK is low during the blanking interval (480 to 524) for normal operation */
/* when LINES_REDUCED is active, the top 40 and bottom 40 are also forced to be blank */

VPORCH = VCOUNTER:['d'479..'d'524];
VPORCH_EXTRA = VCOUNTER:['d'439..'d'525] # VCOUNTER:['d'0..'d'39];

/* VBLANK IS ACTIVE LOW */

VBLANK.D = (LINES_REDUCED & !VPORCH_EXTRA)
          # (!LINES_REDUCED & !VPORCH);
VBLANK.CK = HSYNC;

/* VSYNC is low during the sync pulse interval (490 to 491) */
VSYNC.D = !(VCOUNTER:['d'490..'d'491]);
VSYNC.CK = HSYNC;

VINT_n.D = !((LINES_REDUCED & VCOUNTER:['d'440])
         # (!LINES_REDUCED & VCOUNTER:['d'480]));
VINT_n.CK = HSYNC;
